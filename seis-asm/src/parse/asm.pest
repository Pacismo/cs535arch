WHITESPACE = _{ " " | "\t" }

comment = _{ "//" ~ (!NEWLINE ~ ANY)* }

program = _{ SOI ~ (line? ~ comment?) ~ (NEWLINE ~ line? ~ comment?)* ~ EOI }

line = _{ constant | instruction | directive | label }

constant = {
    const ~ "=" ~ ((float) | (integer ~ (":" ~ type)?) | (char) | (string))
}

instruction = {
    controlop
  | integerop
  | floatop
  | registerop
}

directive = {
    "#[" ~ ident ~ ("=" ~ (integer | string | zpgaddr))? ~ "]"
}

controlop = _{
    halt
  | nop
  | jmp
  | jsr
  | ret
  | jeq
  | jne
  | jgt
  | jlt
  | jge
  | jle
}

integerop = _{
    add
  | sub
  | mul
  | dvu
  | dvs
  | mod
  | and
  | ior
  | xor
  | not
  | sxt
  | bsl
  | bsr
  | asr
  | rol
  | ror
  | cmp
  | tst
}

floatop = _{
    fadd
  | fsub
  | fmul
  | fdiv
  | fmod
  | fcmp
  | fneg
  | frec
  | itof
  | ftoi
  | fchk
}

registerop = _{
    push
  | pop
  | lbr
  | sbr
  | lsr
  | ssr
  | llr
  | slr
  | tfr
  | ldr
  | load
}

ident    = ${ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
label    = ${ ident ~ ":" }
const    = @{ "." ~ ident }
constref = @{ "&" ~ ident }

byte  = ${ ^"byte" }
short = ${ ^"short" }
word  = ${ ^"word" | ^"long" }
type  = _{ byte | short | word }

vareg  = ${ (^"V" ~ ('0'..'9' | 'a'..'f' | 'A'..'F')) }
stareg = ${ ^"SP" | ^"BP" }
lpreg  = ${ ^"LP" }
pcreg  = ${ ^"PC" }
psreg  = ${ ^"PS" }
fsreg  = ${ ^"FS" }
spreg  = _{ pcreg | psreg | fsreg }

reg = _{
    vareg
  | stareg
  | lpreg
}

sign      = ${ "+" | "-" }
dec       = ${ "0" | (ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*) }
hex       = ${ ^"0x" ~ ASCII_HEX_DIGIT+ }
oct       = ${ "0" ~ ASCII_OCT_DIGIT+ }
integer   = @{ sign? ~ uinteger }
uinteger  = _{ hex | oct | dec }
character = @{
    (!"\\" ~ ANY)
  | ("\\" ~ ("\"" | "\\" | "'" | "/" | "b" | "f" | "n" | "r" | "t"))
  | ("\\" ~ ("u" ~ ASCII_HEX_DIGIT{4}))
}

int    = @{ "#" ~ integer }
float  = ${ sign? ~ ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* ~ "." ~ ASCII_DIGIT* ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)? }
string = @{ "\"" ~ (!"\"" ~ character)* ~ "\"" }
char   = @{ "'" ~ (!"\'" ~ character) ~ "'" }

zpgaddr = ${ "@" ~ uinteger }
zpgref  = ${ "&" ~ uinteger }

halt = @{ ^"HALT" }
nop  = @{ ^"NOP" }
jmp  = ${ ^"JMP" ~ WHITESPACE+ ~ jump }
jsr  = ${ ^"JSR" ~ WHITESPACE+ ~ jump }
ret  = @{ ^"RET" }
jeq  = ${ ^"JEQ" ~ WHITESPACE+ ~ jump }
jne  = ${ ^"JNE" ~ WHITESPACE+ ~ jump }
jgt  = ${ ^"JGT" ~ WHITESPACE+ ~ jump }
jlt  = ${ ^"JLT" ~ WHITESPACE+ ~ jump }
jge  = ${ ^"JGE" ~ WHITESPACE+ ~ jump }
jle  = ${ ^"JLE" ~ WHITESPACE+ ~ jump }

jump     = _{ absolute | relative | ident }
absolute = @{ vareg }
relative = @{ int }

add = @{ ^"ADD" ~ WHITESPACE+ ~ int_binop }
sub = @{ ^"SUB" ~ WHITESPACE+ ~ int_binop }
mul = @{ ^"MUL" ~ WHITESPACE+ ~ int_binop }
dvu = @{ ^"DVU" ~ WHITESPACE+ ~ int_binop }
dvs = @{ ^"DVS" ~ WHITESPACE+ ~ int_binop }
mod = @{ ^"MOD" ~ WHITESPACE+ ~ int_binop }
and = @{ ^"AND" ~ WHITESPACE+ ~ int_binop }
ior = @{ ^"IOR" ~ WHITESPACE+ ~ int_binop }
xor = @{ ^"XOR" ~ WHITESPACE+ ~ int_binop }
not = @{ ^"NOT" ~ WHITESPACE+ ~ int_unop }
sxt = @{ ^"SXT" ~ WHITESPACE+ ~ type ~ WHITESPACE+ ~ vareg }
bsl = @{ ^"BSL" ~ WHITESPACE+ ~ int_binop }
bsr = @{ ^"BSR" ~ WHITESPACE+ ~ int_binop }
asr = @{ ^"ASR" ~ WHITESPACE+ ~ int_binop }
rol = @{ ^"ROL" ~ WHITESPACE+ ~ int_binop }
ror = @{ ^"ROR" ~ WHITESPACE+ ~ int_binop }
cmp = @{ ^"CMP" ~ WHITESPACE+ ~ int_cmpop }
tst = @{ ^"TST" ~ WHITESPACE+ ~ int_cmpop }

int_binop = @{ vareg ~ WHITESPACE* ~ "," ~ WHITESPACE* ~ (vareg | int | const) ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
int_unop  = @{ vareg ~ WHITESPACE* ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
int_cmpop = @{ vareg ~ WHITESPACE* ~ "," ~ WHITESPACE* ~ (vareg | int | const) }

fadd = @{ ^"FADD" ~ WHITESPACE+ ~ float_binop }
fsub = @{ ^"FSUB" ~ WHITESPACE+ ~ float_binop }
fmul = @{ ^"FMUL" ~ WHITESPACE+ ~ float_binop }
fdiv = @{ ^"FDIV" ~ WHITESPACE+ ~ float_binop }
fmod = @{ ^"FMOD" ~ WHITESPACE+ ~ float_binop }
fcmp = @{ ^"FCMP" ~ WHITESPACE+ ~ float_cmpop }
fneg = @{ ^"FNEG" ~ WHITESPACE+ ~ float_unop }
frec = @{ ^"FREC" ~ WHITESPACE+ ~ float_unop }
itof = @{ ^"ITOF" ~ WHITESPACE+ ~ float_unop }
ftoi = @{ ^"FTOI" ~ WHITESPACE+ ~ float_unop }
fchk = @{ ^"FCHK" ~ WHITESPACE+ ~ vareg }

float_cmpop = @{ vareg ~ WHITESPACE* ~ "," ~ WHITESPACE* ~ vareg }
float_binop = @{ vareg ~ WHITESPACE* ~ "," ~ WHITESPACE* ~ vareg ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
float_unop  = @{ vareg ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }

push = ${ ^"PUSH" ~ WHITESPACE+ ~ (regstack | uinteger) }
pop  = ${ ^"POP" ~ WHITESPACE+ ~ (regstack | uinteger) }

regstack = ${ "{" ~ WHITESPACE* ~ reg ~ WHITESPACE* ~ ("," ~ WHITESPACE* ~ reg)* ~ WHITESPACE* ~ "}" }

lbr = @{ ^"LBR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
sbr = @{ ^"SBR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
lsr = @{ ^"LSR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
ssr = @{ ^"SSR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
llr = @{ ^"LLR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }
slr = @{ ^"SLR" ~ WHITESPACE+ ~ loadsrc ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }

loadsrc   = @{ zpgaddr | vareg | offsetind | indexind | stackoff }
offsetind = ${ vareg ~ WHITESPACE* ~ "+" ~ WHITESPACE* ~ int }
indexind  = ${ vareg ~ WHITESPACE* ~ "[" ~ WHITESPACE* ~ vareg ~ WHITESPACE* ~ "]" }
stackoff  = ${ "%" ~ uinteger }

/// `TFT reg => reg`
tfr = @{ ^"TFR" ~ WHITESPACE+ ~ (reg | spreg) ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ reg }

/// `LDR #IMM => vareg<.s>`
/// `LDR #IMM =| vareg<.s>`
/// `LDR &ZPA => vareg`
ldr = @{ ^"LDR" ~ WHITESPACE+ ~ (zpaload | immload) }

/// `LOAD constant => vareg`, where `constant` is an `int`, a `float`, a `label`, or a `const` (or a reference to it).
/// This expands to at least one LDR to load the value
load = @{ ^"LOAD" ~ WHITESPACE+ ~ (int | float | label | const | constref) ~ WHITESPACE* ~ assign ~ WHITESPACE* ~ vareg }

zpaload = !{ zpgref ~ assign ~ vareg }
immload = !{ int ~ (assign | insert) ~ vareg ~ ("." ~ part)? }

part = ${ "0" | "1" }

assign = ${ "=>" | "->" }
insert = ${ "=|" }
